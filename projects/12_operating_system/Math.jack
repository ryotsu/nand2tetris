// This file is part of jww.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {
    static Array twoExp;

    /** Initializes the library. */
    function void init() {
        let twoExp = Array.new(16);
        let twoExp[0] = 1;
        let twoExp[1] = 2;
        let twoExp[2] = 4;
        let twoExp[3] = 8;
        let twoExp[4] = 16;
        let twoExp[5] = 32;
        let twoExp[6] = 64;
        let twoExp[7] = 128;
        let twoExp[8] = 256;
        let twoExp[9] = 512;
        let twoExp[10] = 1024;
        let twoExp[11] = 2048;
        let twoExp[12] = 4096;
        let twoExp[13] = 8192;
        let twoExp[14] = 16384;
        let twoExp[15] = 16384 + 16384;

        return;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x < 0) {
            return -x;
        }

        return x;
    }

    function boolean bit(int num, int index) {
        return ~((num & twoExp[index]) = 0);
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {
        var int sum, shiftedX, j;

        let sum = 0;
        let shiftedX = x;
        let j = 0;

        while (j < 16) {
            if (Math.bit(y, j)) {
                let sum = sum + shiftedX;
            }

            let shiftedX = shiftedX + shiftedX;
            let j = j + 1;
        }

        return sum;
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        var int q, result;
        var boolean pos;

        let pos = ((x < 0) = (y < 0));

        let x = Math.abs(x);
        let y = Math.abs(y);

        if (y > x) {
            return 0;
        }

        let q = Math.divide(x, y + y);
        if ((x - (2 * q * y)) < y) {
            let result = q + q;
        } else {
            let result = q + q + 1;
        }

        if (pos) {
            return result;
        } else {
            return -result;
        }
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int y, j, temp1, temp2;

        let y = 0;
        let j = 7;

        while (~ (j < 0)) {
            let temp1 = y + twoExp[j];
            let temp2 = temp1 * temp1;
            if (~(temp2 > x) & (temp2 > 0)) {
                let y = temp1;
            }
            let j = j - 1;
        }

        return y;
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if (a > b) {
            return a;
        } else {
            return b;
        }
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if (a < b) {
            return a;
        } else {
            return b;
        }
    }
}
